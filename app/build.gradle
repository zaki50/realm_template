apply plugin: 'com.android.application'
apply plugin: 'ext-properties'
apply plugin: 'realm-android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.example.realm_template"
        minSdkVersion 9
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        // debug {
        //     storeFile rootProject.file("debug.keystore")
        // }

        release
    }

    buildTypes {
        //noinspection GroovyMissingReturnStatement
        release {
            minifyEnabled true
            shrinkResources true // shrinkResources true requires minifyEnabled true

            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile file('proguard-rules.pro')

            // Configure release signing if ext.properties provides certificate information
            // To enable release signing, copy ext.properties.forReleaseSigning to ext.properties and edit.
            if (project.hasProperty('storeFile')) {
                android.signingConfigs.release.storeFile = rootProject.file(storeFile)
                android.signingConfigs.release.storePassword = storePassword
                android.signingConfigs.release.keyAlias = keyAlias
                android.signingConfigs.release.keyPassword = keyPassword

                signingConfig signingConfigs.release
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

repositories {
    // for stetho-realm
    maven {
        url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo'
    }
}

project.ext {
    supportPackageVersion = '24.1.1'
    supportTestVersion = '0.5'
    espressoVersion = '2.2.1'
    junitVersion = '4.12'
    dexmakerVersion = '1.2'
    powerMockVersion = '1.6.2'
    daggerVersion = '2.0.1'
    leakcanaryVersion = '1.3.1'
}

//noinspection GroovyAssignabilityCheck
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:${supportPackageVersion}"
    compile "com.android.support:recyclerview-v7:${supportPackageVersion}"
    compile "com.android.support:support-annotations:${supportPackageVersion}"
    provided 'javax.annotation:javax.annotation-api:1.2'

    compile "com.google.dagger:dagger:${daggerVersion}"
    apt "com.google.dagger:dagger-compiler:${daggerVersion}"

    debugCompile 'com.facebook.stetho:stetho:1.3.0'
    debugCompile 'com.uphyca:stetho_realm:0.9.0'

    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanaryVersion}"
    debugCompile "com.squareup.leakcanary:leakcanary-android:${leakcanaryVersion}"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanaryVersion}"

    androidTestApt "com.google.dagger:dagger-compiler:${daggerVersion}"
    androidTestCompile "junit:junit:${junitVersion}"
    androidTestCompile "com.android.support.test.espresso:espresso-core:${espressoVersion}"
    androidTestCompile "com.android.support.test:runner:${supportTestVersion}"
    androidTestCompile "com.android.support.test:rules:${supportTestVersion}"
    androidTestCompile "com.android.support:support-annotations:${supportPackageVersion}"

    // mockito for dex
    androidTestCompile "com.google.dexmaker:dexmaker:${dexmakerVersion}"
    androidTestCompile "com.google.dexmaker:dexmaker-mockito:${dexmakerVersion}"

    testCompile "junit:junit:${junitVersion}"
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile "com.android.support.test:runner:${supportTestVersion}"
    testCompile "com.android.support.test:rules:${supportTestVersion}"
    testCompile "com.android.support:support-annotations:${supportPackageVersion}"

    testCompile "org.powermock:powermock-module-junit4:${powerMockVersion}"
    testCompile "org.powermock:powermock-module-junit4-rule:${powerMockVersion}"
    testCompile("org.powermock:powermock-api-mockito:${powerMockVersion}") {
        // to exclude wrong version of hamcrest-core. use mockito-core instead.
        exclude module: 'mockito-all'
    }
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile "org.powermock:powermock-classloading-xstream:${powerMockVersion}"
    // rxjava is required even if it's not used since PowerMock requires all referenced classes.
    testCompile 'io.reactivex:rxjava:1.1.0'
}
