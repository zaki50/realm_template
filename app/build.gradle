apply plugin: 'com.android.application'
apply plugin: 'ext-properties'
apply plugin: 'realm-android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.example.realm_template"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        // debug {
        //     storeFile rootProject.file("debug.keystore")
        // }

        release
    }

    buildTypes {
        //noinspection GroovyMissingReturnStatement
        release {
            minifyEnabled true
            shrinkResources true // shrinkResources true requires minifyEnabled true

            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile file('proguard-rules.pro')

            // Configure release signing if ext.properties provides certificate information
            // To enable release signing, copy ext.properties.forReleaseSigning to ext.properties and edit.
            if (project.hasProperty('storeFile')) {
                android.signingConfigs.release.storeFile = rootProject.file(storeFile)
                android.signingConfigs.release.storePassword = storePassword
                android.signingConfigs.release.keyAlias = keyAlias
                android.signingConfigs.release.keyPassword = keyPassword

                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        encoding = "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

repositories {
    // for stetho-realm
    maven {
        url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo'
    }
}

realm {
    syncEnabled = true
}

project.ext {
    supportPackageVersion = '27.0.0'
    supportTestVersion = '1.0.0'
    espressoVersion = '3.0.1'
    junitVersion = '4.12'
    dexmakerVersion = '1.2'
    powerMockVersion = '1.6.2'
    daggerVersion = '2.0.1'
    leakcanaryVersion = '1.3.1'
}

//noinspection GroovyAssignabilityCheck
dependencies {
    annotationProcessor 'dk.ilios:realmfieldnameshelper:1.1.0'

    implementation "com.android.support:appcompat-v7:${supportPackageVersion}"
    implementation "com.android.support:support-v4:${supportPackageVersion}"
    implementation "com.android.support:recyclerview-v7:${supportPackageVersion}"
    implementation "com.android.support:support-annotations:${supportPackageVersion}"
    compileOnly    'javax.annotation:javax.annotation-api:1.2'

    implementation 'io.realm:android-adapters:2.1.0'

    implementation "com.google.dagger:dagger:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

    debugImplementation 'com.facebook.stetho:stetho:1.4.2'
    debugImplementation 'com.uphyca:stetho_realm:2.1.0'

    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanaryVersion}"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${leakcanaryVersion}"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanaryVersion}"

    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    androidTestImplementation "junit:junit:${junitVersion}"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${espressoVersion}"
    androidTestImplementation "com.android.support.test:runner:${supportTestVersion}"
    androidTestImplementation "com.android.support.test:rules:${supportTestVersion}"
    androidTestImplementation "com.android.support:support-annotations:${supportPackageVersion}"

    // mockito for dex
    androidTestImplementation "com.google.dexmaker:dexmaker:${dexmakerVersion}"
    androidTestImplementation "com.google.dexmaker:dexmaker-mockito:${dexmakerVersion}"

    testImplementation "junit:junit:${junitVersion}"
    testImplementation 'org.robolectric:robolectric:3.0'
    testImplementation "com.android.support.test:runner:${supportTestVersion}"
    testImplementation "com.android.support.test:rules:${supportTestVersion}"
    testImplementation "com.android.support:support-annotations:${supportPackageVersion}"

    testImplementation "org.powermock:powermock-module-junit4:${powerMockVersion}"
    testImplementation "org.powermock:powermock-module-junit4-rule:${powerMockVersion}"
    testImplementation("org.powermock:powermock-api-mockito:${powerMockVersion}") {
        // to exclude wrong version of hamcrest-core. use mockito-core instead.
        exclude module: 'mockito-all'
    }
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation "org.powermock:powermock-classloading-xstream:${powerMockVersion}"
    // rxjava is required even if it's not used since PowerMock requires all referenced classes.
    testImplementation 'io.reactivex:rxjava:1.1.7'
}
